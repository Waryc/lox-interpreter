use crate::{ast, lexer, error::LoxError::*, report_error};

grammar;

pub Program: ast::Program = <stmts:Decl*> => ast::Program { <> };

// Declarations
Decl = {
    DeclClass,
    DeclFun,
    DeclVar,
    Stmt,
}

DeclClass: ast::Stmt = {
    "class" <name:identifier> <super_:("<" <ExprVar>)?> "{" <methods:Function*> "}" =>
        ast::Stmt::Class(ast::StmtClass { <> }),
    "class" <identifier> <!> => report_error!(SuperclassNotFound),
}

DeclFun: ast::Stmt = "fun" <function:Function> => ast::Stmt::Fun(<>);

DeclVar: ast::Stmt = {
    "var" <name:identifier> <value:("=" <Expr>)?> ";" =>
    ast::Stmt::Var(ast::StmtVar {
        var: ast::Var { name },
        value,
    }),
    "var" <err:!> => report_error!(DeclVarMissingVariableName(err.error)),
}

Stmt: ast::Stmt = {
    StmtOpen,
    StmtClosed,
}

StmtOpen: ast::Stmt = {
    "if" "(" <cond:Expr> ")" <then:Stmt> =>
        ast::Stmt::If(Box::new(ast::StmtIf { <>, else_: None })),
    "if" "(" <cond:Expr> ")" <then:StmtClosed> "else" <else_:StmtOpen> =>
        ast::Stmt::If(Box::new(ast::StmtIf { cond, then, else_: Some(else_) })),
    "while" "(" <cond:Expr> ")" <body:StmtOpen> =>
        ast::Stmt::While(Box::new(ast::StmtWhile { <> })),
    "for" "(" <init:ForInit> <cond:ForCond> <incr:ForIncr> ")" <body:StmtOpen> =>
        ast::Stmt::For(Box::new(ast::StmtFor { <> })),
}

StmtClosed: ast::Stmt = {
    "if" "(" <cond:Expr> ")" <then:StmtClosed> "else" <else_:StmtClosed> =>
        ast::Stmt::If(Box::new(ast::StmtIf { cond, then, else_: Some(else_) })),
    "while" "(" <cond:Expr> ")" <body:StmtClosed> =>
        ast::Stmt::While(Box::new(ast::StmtWhile { <> })),
    "for" "(" <init:ForInit> <cond:ForCond> <incr:ForIncr> ")" <body:StmtClosed> =>
        ast::Stmt::For(Box::new(ast::StmtFor { <> })),
    StmtSimple,
}

ForInit: Option<ast::Stmt> = {
    DeclVar => Some(<>),
    StmtExpr => Some(<>),
    ";" => None,
}

ForCond = <Expr?> ";";

ForIncr = <Expr?>;

StmtSimple = {
    StmtBlock,
    StmtExpr,
    StmtPrint,
    StmtReturn,
}

StmtBlock: ast::Stmt = StmtBlockInternal => ast::Stmt::Block(<>);

StmtBlockInternal: ast::StmtBlock = {
    "{" <stmts:Decl*> "}" => ast::StmtBlock { <> },
    "{" <err:!> => report_error!(BlockMissingRightBrace(err.error)),
}

StmtExpr: ast::Stmt = {
    <value:Expr> ";" => ast::Stmt::Expr(ast::StmtExpr { <> }),
    <_a:Expr> <err:!> => report_error!(StmtMissingSemicolon(err.error)),
}

StmtPrint: ast::Stmt = {
    "print" <value:Expr> ";" => ast::Stmt::Print(ast::StmtPrint { <> }),
    "print" <_a:Expr> <err:!> => report_error!(StmtMissingSemicolon(err.error)),
}

StmtReturn: ast::Stmt = {
    "return" <value:Expr?> ";" => ast::Stmt::Return(ast::StmtReturn { <> }),
    "return" <_a:Expr> <err:!> => report_error!(StmtMissingSemicolon(err.error)),
}

// Expressions
Expr = ExprAssign;

ExprAssign = {
    <name:identifier> "=" <value:Expr> =>
        ast::Expr::Assign(Box::new(ast::ExprAssign {
            var: ast::Var { name },
            value,
        })),
    <object:ExprCall> "." <name:identifier> "=" <value:Expr> =>
        ast::Expr::Set(Box::new(ast::ExprSet { <> })),
    ExprLogicOr,
}

ExprLogicOr = ExprInfix<ExprLogicOr, OpLogicOr, ExprLogicAnd>;
OpLogicOr: ast::OpInfix = "or" => ast::OpInfix::LogicOr;

ExprLogicAnd = ExprInfix<ExprLogicAnd, OpLogicAnd, ExprEquality>;
OpLogicAnd: ast::OpInfix = "and" => ast::OpInfix::LogicAnd;

ExprEquality = ExprInfix<ExprEquality, OpEquality, ExprComparison>;
OpEquality: ast::OpInfix = {
    "==" => ast::OpInfix::Equal,
    "!=" => ast::OpInfix::NotEqual,
}

ExprComparison = ExprInfix<ExprComparison, OpComparison, ExprTerm>;
OpComparison: ast::OpInfix = {
    ">" => ast::OpInfix::Greater,
    ">=" => ast::OpInfix::GreaterEqual,
    "<" => ast::OpInfix::Less,
    "<=" => ast::OpInfix::LessEqual,
}

ExprTerm = ExprInfix<ExprTerm, OpTerm, ExprFactor>;
OpTerm: ast::OpInfix = {
    "+" => ast::OpInfix::Add,
    "-" => ast::OpInfix::Subtract,
}

ExprFactor = ExprInfix<ExprFactor, OpFactor, ExprPrefix>;
OpFactor: ast::OpInfix = {
    "*" => ast::OpInfix::Multiply,
    "/" => ast::OpInfix::Divide,
}

ExprPrefix: ast::Expr = {
    <op:OpPrefix> <rt:ExprPrefix> =>
        ast::Expr::Prefix(Box::new(ast::ExprPrefix { <> })),
    ExprCall,
}
OpPrefix: ast::OpPrefix = {
    "-" => ast::OpPrefix::Negate,
    "!" => ast::OpPrefix::Not,
}

ExprCall: ast::Expr = {
    <callee:ExprCall> "(" <args:Args> ")" =>
        ast::Expr::Call(Box::new(ast::ExprCall { callee, args })),
    <object:ExprCall> "." <name:identifier> =>
        ast::Expr::Get(Box::new(ast::ExprGet { <> })),
    "super" "." <name:identifier> =>
        ast::Expr::Super(ast::ExprSuper {
            super_: ast::Var {
                name: "super".to_string(),
            },
            name,
        }),
    ExprPrimary,
}

ExprPrimary: ast::Expr = {
    // Literals
    "nil" => ast::Expr::Literal(ast::ExprLiteral::Nil),
    "false" => ast::Expr::Literal(ast::ExprLiteral::Bool(false)),
    "true" => ast::Expr::Literal(ast::ExprLiteral::Bool(true)),
    string => ast::Expr::Literal(ast::ExprLiteral::String(<>[1..(<>.len()-1)].to_string())),
    number => ast::Expr::Literal(ast::ExprLiteral::Number(<>)),

    // Variables
    ExprVar,
    ExprThis,

    // Grouping
    "(" <Expr> ")",
}

ExprVar: ast::Expr = <name:identifier> =>
    ast::Expr::Var(ast::ExprVar { var: ast::Var { name } });
ExprThis: ast::Expr = "this" => ast::Expr::Var(ast::ExprVar { var: ast::Var {
    name: "this".to_string(),
}});

Function: ast::StmtFun = {
    <name:identifier> "(" <params:Params> ")" <body:StmtBlockInternal> => ast::StmtFun { <> },
    <_a:identifier> "(" <_b:Params> <err:!> => panic!("{}", FunctionMissingRightParen(err.error)),
};

Params: Vec<String> = {
    <first:identifier> <mut params:("," <identifier>)*> => {
        params.insert(0, first);
        params
    },
    () => Vec::new(),
};

Args: Vec<ast::Expr> = {
    <first:Expr> <mut args:("," <Expr>)*> => {
        args.insert(0, first);
        args
    },
    () => Vec::new(),
}

ExprInfix<Lt, Op, Rt>: ast::Expr = {
    <lt:Lt> <op:Op> <rt:Rt> =>
        ast::Expr::Infix(Box::new(ast::ExprInfix { <> })),
    Rt,
}

extern {
    enum lexer::Token {
        // Single-character tokens.
        "(" => lexer::Token::LtParen,
        ")" => lexer::Token::RtParen,
        "{" => lexer::Token::LtBrace,
        "}" => lexer::Token::RtBrace,
        "," => lexer::Token::Comma,
        "." => lexer::Token::Dot,
        "-" => lexer::Token::Minus,
        "+" => lexer::Token::Plus,
        ";" => lexer::Token::Semicolon,
        "/" => lexer::Token::Slash,
        "*" => lexer::Token::Asterisk,

        // One or two character tokens.
        "!" => lexer::Token::Bang,
        "!=" => lexer::Token::BangEqual,
        "=" => lexer::Token::Equal,
        "==" => lexer::Token::EqualEqual,
        ">" => lexer::Token::Greater,
        ">=" => lexer::Token::GreaterEqual,
        "<" => lexer::Token::Less,
        "<=" => lexer::Token::LessEqual,

        // Literals.
        identifier => lexer::Token::Identifier(<String>),
        string => lexer::Token::String(<String>),
        number => lexer::Token::Number(<f64>),

        // Keywords.
        "and" => lexer::Token::And,
        "class" => lexer::Token::Class,
        "else" => lexer::Token::Else,
        "false" => lexer::Token::False,
        "for" => lexer::Token::For,
        "fun" => lexer::Token::Fun,
        "if" => lexer::Token::If,
        "nil" => lexer::Token::Nil,
        "or" => lexer::Token::Or,
        "print" => lexer::Token::Print,
        "return" => lexer::Token::Return,
        "super" => lexer::Token::Super,
        "this" => lexer::Token::This,
        "true" => lexer::Token::True,
        "var" => lexer::Token::Var,
        "while" => lexer::Token::While,
    }
}
